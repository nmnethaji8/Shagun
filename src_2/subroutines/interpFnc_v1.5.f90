!!------------------------- MLPG_GET_ETA --------------------------!!
SUBROUTINE MLPG_GET_ETA(FSDOM, NFS, XFS, YFS, ZFS, NOT, XOT, YOT,& 
  ZOT, ERR, DDL, NLMAX, PRINTERRMSG)
USE NODELINKMOD
IMPLICIT NONE
  
  TYPE(NODELINKTYP),INTENT(IN)::FSDOM
  INTEGER(KIND=4),INTENT(IN)::NFS,NOT,NLMAX, PRINTERRMSG
  REAL(KIND=8),INTENT(IN)::XFS(NFS),YFS(NFS),ZFS(NFS)
  REAL(KIND=8),INTENT(IN)::XOT(NOT),YOT(NOT),DDL
  INTEGER(KIND=4),INTENT(OUT)::ERR(NOT)
  REAL(KIND=8),INTENT(OUT)::ZOT(NOT)

  INTEGER(KIND=4)::INOD,ND(0:NLMAX),NN
  INTEGER(KIND=4)::I,J,K,I2,NI
  INTEGER(KIND=4)::IX,IY,IZ,IPOS,IX1,IY1,IZ1
  INTEGER(KIND=4)::PLANEID=1
  REAL(KIND=8)::PHI(NLMAX),W(NLMAX),B(4,NLMAX)
  REAL(KIND=8)::A(4,4),AINV(4,4),PT(4)
  REAL(KIND=8)::PB2(4),PP2(4,4)
  REAL(KIND=8)::XQ,YQ,ZQ,AA(4,NLMAX),PINT
  REAL(KIND=8)::RIAV,DR,DR2,ETMP,WWI

  !! [NOTE]
  !! ERR=0 if the Z can be interpolated at the requested (X,Y).
  !! Otherwise ERR=1. Z=0.

  ! PRINTERRMSG = 0 : DOESNT PRINT ERROR MESSAGES

  WRITE(8,'(" [MSG] ENTERING MLPG_GET_ETA")')  

  RIAV=2.1D0*DDL
  
  ZOT=0D0
  ERR=0

  !$OMP PARALLEL DEFAULT(PRIVATE) SHARED(NOT,XOT,YOT,ZOT,FSDOM,&
  !$OMP&  NFS,XFS,YFS,ZFS,NLMAX,RIAV,PLANEID,ERR, PRINTERRMSG)
  !$OMP DO SCHEDULE(DYNAMIC,50)
  DO INOD=1,NOT    

    XQ=XOT(INOD)
    YQ=YOT(INOD)
    ZQ=0D0

    CALL WEIGHTF3_XY_SHA(FSDOM,NLMAX,NFS,XFS,YFS,XQ,YQ,&
      NN,ND,W,1,I,I2,IX,IY,IZ,IPOS,IX1,IY1,IZ1,RIAV,&
      ETMP,DR,DR2,WWI)    

    IF(NN.LE.0)THEN
      IF(PRINTERRMSG.NE.0)THEN
        WRITE(8,'("     [ERR] NO NEGH IN MLPG_GET_ETA FOR POI")')
        WRITE(8,'("     [---] ",2F15.6)')XQ,YQ
      ENDIF
      ZOT(INOD)=0D0
      ERR(INOD)=1
      CYCLE
    ENDIF

    IF(NN.LE.3)THEN
      CALL SHEPARDSF_SHA(PHI,NN,W,NLMAX,I,WWI)
      GOTO 30
    ENDIF

    CALL SHAPPARA_R_2D_SHA(NFS,4,NLMAX,A,B,NN,ND,W,&
      XFS,YFS,PLANEID,I,NI,J,K,PB2,PP2,WWI,ETMP)

    CALL BASEFUN_SHA(4,PT,XQ,YQ,ZQ)

    A(4,4)=1D0

    CALL FINDINV4X4(A,AINV,WWI)    
    IF(ABS(WWI).LT.1E-15)THEN
      IF(PRINTERRMSG.NE.0)THEN
        WRITE(8,'("     [ERR] SINGULAR MATRIX A, ADET ",F15.6)')WWI
        WRITE(8,'("     [---] LOC ",3F15.6)')XQ,YQ,ZQ
      ENDIF
      CALL SHEPARDSF_SHA(PHI,NN,W,NLMAX,I,WWI)
      GOTO 30
    ENDIF

    CALL SHAPEFUN_PHI_SHA(4,NLMAX,NN,AINV,B,PT,AA,PHI,I,J,K)
    

30  WWI=0D0
    DO I2=1,NN
      WWI=WWI+PHI(I2)*ZFS(ND(I2))
    ENDDO
    ZOT(INOD)=WWI

  ENDDO
  !$OMP END DO NOWAIT
  !$OMP END PARALLEL

  WRITE(8,'(" [MSG] EXITING MLPG_GET_ETA")')
  WRITE(8,*)

END SUBROUTINE MLPG_GET_ETA
!!----------------------- END MLPG_GET_ETA ------------------------!!



!!-------------------------- MLPG_GET_UP --------------------------!!
SUBROUTINE MLPG_GET_UP(DOMIN,NIN,XIN,YIN,ZIN,UIN,VIN,WIN,PIN,&
  NOT,XOT,YOT,ZOT,UOT,VOT,WOT,POT,DDL,NLMAX)
USE NODELINKMOD
IMPLICIT NONE
  
  TYPE(NODELINKTYP),INTENT(IN)::DOMIN
  INTEGER(KIND=4),INTENT(IN)::NIN,NOT,NLMAX
  REAL(KIND=8),INTENT(IN)::XIN(NIN),YIN(NIN),ZIN(NIN)
  REAL(KIND=8),INTENT(IN)::UIN(NIN),VIN(NIN),WIN(NIN),PIN(NIN)
  REAL(KIND=8),INTENT(IN)::XOT(NOT),YOT(NOT),ZOT(NOT),DDL
  REAL(KIND=8),INTENT(OUT)::UOT(NOT),VOT(NOT),WOT(NOT),POT(NOT)

  INTEGER(KIND=4)::INOD,ND(0:NLMAX),NN
  INTEGER(KIND=4)::I,J,K,I2,NI
  INTEGER(KIND=4)::IX,IY,IZ,IPOS,IX1,IY1,IZ1
  REAL(KIND=8)::PHI(NLMAX),W(NLMAX),B(4,NLMAX)
  REAL(KIND=8)::A(4,4),AINV(4,4),PT(4)
  REAL(KIND=8)::PB2(4),PP2(4,4)
  REAL(KIND=8)::XQ,YQ,ZQ,AA(4,NLMAX),PINT
  REAL(KIND=8)::RIAV,DR,DR2,ETMP,WWI
  REAL(KIND=8)::PTMP,UTMP,VTMP,WTMP

  WRITE(8,'(" [MSG] ENTERING MLPG_GET_UP")')

  RIAV=2.1D0*DDL

  !$OMP PARALLEL DEFAULT(PRIVATE) SHARED(NLMAX,RIAV,NIN,&
  !$OMP&  XIN,YIN,ZIN,UIN,VIN,WIN,PIN,NOT,XOT,YOT,ZOT,UOT,&
  !$OMP&  VOT,WOT,POT,DOMIN)
  !$OMP DO SCHEDULE(DYNAMIC,100)
  DO INOD=1,NOT    

    XQ=XOT(INOD)
    YQ=YOT(INOD)
    ZQ=ZOT(INOD)

    CALL WEIGHTF3_SHA(DOMIN,NLMAX,NIN,XIN,YIN,ZIN,XQ,YQ,ZQ,&
      NN,ND,W,1,I,I2,IX,IY,IZ,IPOS,IX1,IY1,IZ1,RIAV,&
      ETMP,DR,DR2,WWI)    

    IF(NN.LE.0)THEN
      WRITE(8,'("     [ERR] NO NEGH IN MLPG_GET_UP FOR POI")')
      WRITE(8,'("     [---] ",3F15.6)')XQ,YQ,ZQ
      UOT(INOD)=0D0
      VOT(INOD)=0D0
      WOT(INOD)=0D0
      POT(INOD)=0D0
      CYCLE
    ENDIF

    IF(NN.LE.3)THEN
      CALL SHEPARDSF_SHA(PHI,NN,W,NLMAX,I,WWI)
      GOTO 30
    ENDIF

    CALL SHAPPARA_R_SHA(NIN,4,NLMAX,A,B,NN,ND,W,&
      XIN,YIN,ZIN,I,NI,J,K,PB2,PP2,WWI)

    CALL BASEFUN_SHA(4,PT,XQ,YQ,ZQ)


    CALL FINDINV4X4(A,AINV,WWI)    
    IF(ABS(WWI).LT.1E-15)THEN
      WRITE(8,'("     [ERR] SINGULAR MATRIX A, ADET ",F15.6)')WWI
      WRITE(8,'("     [---] LOC ",3F15.6)')XQ,YQ,ZQ
      CALL SHEPARDSF_SHA(PHI,NN,W,NLMAX,I,WWI)
      GOTO 30
    ENDIF

    CALL SHAPEFUN_PHI_SHA(4,NLMAX,NN,AINV,B,PT,AA,PHI,I,J,K)
    

30  UTMP=0D0
    VTMP=0D0
    WTMP=0D0
    PTMP=0D0
    DO I2=1,NN
      UTMP=UTMP+PHI(I2)*UIN(ND(I2))
      VTMP=VTMP+PHI(I2)*VIN(ND(I2))
      WTMP=WTMP+PHI(I2)*WIN(ND(I2))
      PTMP=PTMP+PHI(I2)*PIN(ND(I2))
    ENDDO
    UOT(INOD)=UTMP
    VOT(INOD)=VTMP
    WOT(INOD)=WTMP
    POT(INOD)=PTMP

  ENDDO
  !$OMP END DO NOWAIT
  !$OMP END PARALLEL

  WRITE(8,'(" [MSG] EXITING MLPG_GET_UP")')
  WRITE(8,*)

END SUBROUTINE MLPG_GET_UP
!!------------------------ END MLPG_GET_UP ------------------------!!



!!------------------------- MLPG_GET_UP2 --------------------------!!
SUBROUTINE MLPG_GET_UP2(DOMIN, LNODE, NODEID, NWALLID, NIN, &
  XIN, YIN, ZIN, UIN, VIN, WIN, PIN, NOT, XOT, YOT, ZOT, &
  UOT, VOT, WOT, POT, DDL, NLMAX)
USE NODELINKMOD
IMPLICIT NONE
  
  TYPE(NODELINKTYP),INTENT(IN)::DOMIN
  INTEGER(KIND=4),INTENT(IN)::NIN,NOT,NLMAX, LNODE
  INTEGER(KIND=4),INTENT(IN)::NODEID(-7:NIN), NWALLID(LNODE,4)
  REAL(KIND=8),INTENT(IN)::XIN(NIN),YIN(NIN),ZIN(NIN)
  REAL(KIND=8),INTENT(IN)::UIN(NIN),VIN(NIN),WIN(NIN),PIN(NIN)
  REAL(KIND=8),INTENT(IN)::XOT(NOT),YOT(NOT),ZOT(NOT),DDL
  REAL(KIND=8),INTENT(OUT)::UOT(NOT),VOT(NOT),WOT(NOT),POT(NOT)

  INTEGER(KIND=4)::INOD,ND(0:NLMAX),NN
  INTEGER(KIND=4)::I,J,K,I2,NI
  INTEGER(KIND=4)::IX,IY,IZ,IPOS,IX1,IY1,IZ1
  REAL(KIND=8)::PHI(NLMAX),W(NLMAX),B(4,NLMAX)
  REAL(KIND=8)::A(4,4),AINV(4,4),PT(4)
  REAL(KIND=8)::PB2(4),PP2(4,4)
  REAL(KIND=8)::XQ,YQ,ZQ,AA(4,NLMAX),PINT
  REAL(KIND=8)::RIAV,DR,DR2,ETMP,WWI
  REAL(KIND=8)::PTMP,UTMP,VTMP,WTMP

  WRITE(8,'(" [MSG] ENTERING MLPG_GET_UP")')

  RIAV=2.1D0*DDL

  !$OMP PARALLEL DEFAULT(PRIVATE) SHARED(LNODE, NLMAX, RIAV, NIN, &
  !$OMP&  NODEID, NWALLID, XIN, YIN, ZIN, UIN, VIN, WIN, PIN, NOT, &
  !$OMP&  XOT, YOT, ZOT, UOT, VOT, WOT, POT, DOMIN)
  !$OMP DO SCHEDULE(DYNAMIC,100)
  DO INOD=1,NOT    

    XQ=XOT(INOD)
    YQ=YOT(INOD)
    ZQ=ZOT(INOD)

    CALL WEIGHTF4_SHA(DOMIN, LNODE, NLMAX, NIN, NODEID(-7:NIN), &
      NWALLID, XIN, YIN, ZIN, &
      XQ, YQ, ZQ, NN, ND, W, 1, I, I2, IX, IY, IZ, IPOS, &
      IX1, IY1, IZ1, RIAV, ETMP, DR, DR2, WWI)    

    IF(NN.LE.0)THEN
      WRITE(8,'("     [ERR] NO NEGH IN MLPG_GET_UP FOR POI")')
      WRITE(8,'("     [---] ",3F15.6)')XQ,YQ,ZQ
      UOT(INOD)=0D0
      VOT(INOD)=0D0
      WOT(INOD)=0D0
      POT(INOD)=0D0
      CYCLE
    ENDIF

    IF(NN.LE.3)THEN
      CALL SHEPARDSF_SHA(PHI,NN,W,NLMAX,I,WWI)
      GOTO 30
    ENDIF

    CALL SHAPPARA_R_SHA(NIN,4,NLMAX,A,B,NN,ND,W,&
      XIN,YIN,ZIN,I,NI,J,K,PB2,PP2,WWI)

    CALL BASEFUN_SHA(4,PT,XQ,YQ,ZQ)


    CALL FINDINV4X4(A,AINV,WWI)    
    IF(ABS(WWI).LT.1E-15)THEN
      WRITE(8,'("     [ERR] SINGULAR MATRIX A, ADET ",F15.6)')WWI
      WRITE(8,'("     [---] LOC ",3F15.6)')XQ,YQ,ZQ
      CALL SHEPARDSF_SHA(PHI,NN,W,NLMAX,I,WWI)
      GOTO 30
    ENDIF

    CALL SHAPEFUN_PHI_SHA(4,NLMAX,NN,AINV,B,PT,AA,PHI,I,J,K)
    

30  UTMP=0D0
    VTMP=0D0
    WTMP=0D0
    PTMP=0D0
    DO I2=1,NN
      UTMP=UTMP+PHI(I2)*UIN(ND(I2))
      VTMP=VTMP+PHI(I2)*VIN(ND(I2))
      WTMP=WTMP+PHI(I2)*WIN(ND(I2))
      PTMP=PTMP+PHI(I2)*PIN(ND(I2))
    ENDDO
    UOT(INOD)=UTMP
    VOT(INOD)=VTMP
    WOT(INOD)=WTMP
    POT(INOD)=PTMP

  ENDDO
  !$OMP END DO NOWAIT
  !$OMP END PARALLEL

  WRITE(8,'(" [MSG] EXITING MLPG_GET_UP")')
  WRITE(8,*)

END SUBROUTINE MLPG_GET_UP2
!!----------------------- END MLPG_GET_UP2 ------------------------!!



! !!------------------------ MLPG_GET_DEPINT ------------------------!!
! SUBROUTINE MLPG_GET_DEPINT(MLDOM, FSDOM, LNODE, NIN, NODEID, & 
!   NWALLID, XIN, YIN, ZIN, UIN, VIN, WIN, NOT, XOT, YOT, ZOT, &
!   ERR, MLDDL, NLMAX, DZ)
! USE NODELINKMOD
! IMPLICIT NONE
  
!   TYPE(NODELINKTYP),INTENT(IN)::MLDOM, FSDOM
!   INTEGER(KIND=4),INTENT(IN)::NIN, NOT, NLMAX, LNODE, ERR(NOT)
!   INTEGER(KIND=4),INTENT(IN)::NODEID(-7:NIN), NWALLID(LNODE,4)
!   REAL(KIND=8),INTENT(IN)::XIN(NIN), YIN(NIN), ZIN(NIN)
!   REAL(KIND=8),INTENT(IN)::UIN(NIN), VIN(NIN), WIN(NIN)
!   REAL(KIND=8),INTENT(IN)::XOT(NOT), YOT(NOT), ZOT(NOT)
!   REAL(KIND=8),INTENT(IN)::MLDDL, DZ
!   !REAL(KIND=8),INTENT(OUT)::

!   !INTEGER(KIND=4)::
!   INTEGER(KIND=4)::I,J,K,I2,NI    
!   !REAL(KIND=8)::
  
!   WRITE(8,'(" [MSG] ENTERING MLPG_GET_DEPINT")')
  
  
!   ! CALL MLPG_GET_UP2(MLDOM, LNODE, NODEID(-7:NIN), NWALLID, NIN, &
!   !   XIN, YIN, ZIN, UIN, VIN, WIN, PIN, NOT, XOT, YOT, ZOT, &
!   !   UOT, VOT, WOT, POT, MLDDL, NLMAX)
  

!   WRITE(8,'(" [MSG] EXITING MLPG_GET_DEPINT")')
!   WRITE(8,*)

! END SUBROUTINE MLPG_GET_DEPINT
! !!---------------------- END MLPG_GET_DEPINT ----------------------!!



!!------------------------- WEIGHTF3_SHA --------------------------!!
SUBROUTINE WEIGHTF3_SHA(FSDOM,NLMAX,NIN,XIN,YIN,ZIN,XQ,YQ,ZQ,&
  NN,ND,W,IDWEI,I,I2,IX,IY,IZ,IPOS,IX1,IY1,IZ1,RIAV,&
  ETMP,DR,DR2,WWI)    
USE NODELINKMOD
IMPLICIT NONE
  
  TYPE(NODELINKTYP),INTENT(IN)::FSDOM
  INTEGER(KIND=4),INTENT(IN)::NLMAX,NIN,IDWEI
  INTEGER(KIND=4),INTENT(OUT)::NN,ND(0:NLMAX)
  REAL(KIND=8),INTENT(IN)::XIN(NIN),YIN(NIN),ZIN(NIN)
  REAL(KIND=8),INTENT(IN)::XQ,YQ,ZQ
  REAL(KIND=8),INTENT(OUT)::W(NLMAX)

  INTEGER(KIND=4),INTENT(INOUT)::I,I2,IX,IY,IZ,IPOS,IX1,IY1,IZ1
  REAL(KIND=8),INTENT(INOUT)::RIAV,ETMP,DR,DR2,WWI
  
  
  NN=0
  ETMP=DEXP(-(1D0**2))

  CALL FSDOM%FINDCELL(XQ,YQ,ZQ,IX,IY,IZ,IPOS)

  DO IX1=IX-1,IX+1
    DO IY1=IY-1,IY+1
      DO IZ1=IZ-1,IZ+1

        IF((IX1.GE.0.AND.IX1.LT.FSDOM%CELLX).AND.&
          (IY1.GE.0.AND.IY1.LT.FSDOM%CELLY).AND.&
          (IZ1.GE.0.AND.IZ1.LT.FSDOM%CELLZ))THEN

          IPOS=IX1+IY1*FSDOM%CELLX+IZ1*FSDOM%CELLY*FSDOM%CELLX
          
          DO I2=1,FSDOM%CELL(IPOS,0)
            I=FSDOM%CELL(IPOS,I2)
            DR=DSQRT((XIN(I)-XQ)**2 + (YIN(I)-YQ)**2 + (ZIN(I)-ZQ)**2)
            IF(DR.LT.RIAV)THEN
              DR2=DR/RIAV

              IF(IDWEI.EQ.1)THEN
                WWI=(DEXP(-(DR2**2))-ETMP)/(1D0-ETMP)
              ELSE
                WWI=1D0-6D0*(DR2)**2+8D0*(DR2)**3-3D0*(DR2)**4 
              ENDIF

              IF(WWI.LE.0D0)CYCLE
              NN=NN+1
              IF(NN.GT.NLMAX)THEN
                WRITE(8,'(" [ERR] INCREASE NLMAX IN WEIGHTF3_SHA")')
                STOP
              ENDIF
              ND(NN)=I
              W(NN)=WWI
            ENDIF
          ENDDO
            
        ENDIF

      ENDDO
    ENDDO
  ENDDO
  ND(0)=NN

END SUBROUTINE WEIGHTF3_SHA
!!----------------------- END WEIGHTF3_SHA ------------------------!!



!!----------------------- WEIGHTF3_XY_SHA -------------------------!!
SUBROUTINE WEIGHTF3_XY_SHA(FSDOM,NLMAX,NIN,XIN,YIN,XQ,YQ,&
  NN,ND,W,IDWEI,I,I2,IX,IY,IZ,IPOS,IX1,IY1,IZ1,RIAV,&
  ETMP,DR,DR2,WWI)    
USE NODELINKMOD
IMPLICIT NONE
  
  TYPE(NODELINKTYP),INTENT(IN)::FSDOM
  INTEGER(KIND=4),INTENT(IN)::NLMAX,NIN,IDWEI
  INTEGER(KIND=4),INTENT(OUT)::NN,ND(0:NLMAX)
  REAL(KIND=8),INTENT(IN)::XIN(NIN),YIN(NIN)
  REAL(KIND=8),INTENT(IN)::XQ,YQ
  REAL(KIND=8),INTENT(OUT)::W(NLMAX)

  INTEGER(KIND=4),INTENT(INOUT)::I,I2,IX,IY,IZ,IPOS,IX1,IY1,IZ1
  REAL(KIND=8),INTENT(INOUT)::RIAV,ETMP,DR,DR2,WWI
  
  
  NN=0
  ETMP=DEXP(-(1D0**2))

  !! Z=0 FOR FREE-SURFACE DOMAIN
  DR=0
  CALL FSDOM%FINDCELL(XQ,YQ,DR,IX,IY,IZ,IPOS)

  DO IX1=IX-1,IX+1
    DO IY1=IY-1,IY+1
      DO IZ1=IZ-1,IZ+1

        IF((IX1.GE.0.AND.IX1.LT.FSDOM%CELLX).AND.&
          (IY1.GE.0.AND.IY1.LT.FSDOM%CELLY).AND.&
          (IZ1.GE.0.AND.IZ1.LT.FSDOM%CELLZ))THEN

          IPOS=IX1+IY1*FSDOM%CELLX+IZ1*FSDOM%CELLY*FSDOM%CELLX
          
          DO I2=1,FSDOM%CELL(IPOS,0)
            I=FSDOM%CELL(IPOS,I2)
            DR=DSQRT((XIN(I)-XQ)**2 + (YIN(I)-YQ)**2 )
            IF(DR.LT.RIAV)THEN
              DR2=DR/RIAV

              IF(IDWEI.EQ.1)THEN
                WWI=(DEXP(-(DR2**2))-ETMP)/(1D0-ETMP)
              ELSE
                WWI=1D0-6D0*(DR2)**2+8D0*(DR2)**3-3D0*(DR2)**4 
              ENDIF

              IF(WWI.LE.0D0)CYCLE
              NN=NN+1
              IF(NN.GT.NLMAX)THEN
                WRITE(8,'(" [ERR] INCREASE NLMAX IN WEIGHTF3_SHA")')
                STOP
              ENDIF
              ND(NN)=I
              W(NN)=WWI
            ENDIF
          ENDDO
            
        ENDIF

      ENDDO
    ENDDO
  ENDDO
  ND(0)=NN

END SUBROUTINE WEIGHTF3_XY_SHA
!!--------------------- END WEIGHTF3_XY_SHA -----------------------!!



!!------------------------- WEIGHTF4_SHA --------------------------!!
SUBROUTINE WEIGHTF4_SHA(FSDOM, LNODE, NLMAX, NIN, NODEID, NWALLID, &
  XIN, YIN, ZIN, XQ, YQ, ZQ, NN, ND, W, IDWEI, I, I2, IX, IY, IZ, &
  IPOS, IX1, IY1, IZ1, RIAV, ETMP, DR, DR2, WWI)    
USE NODELINKMOD
IMPLICIT NONE
  
  TYPE(NODELINKTYP),INTENT(IN)::FSDOM
  INTEGER(KIND=4),INTENT(IN)::NLMAX,NIN,IDWEI, LNODE
  INTEGER(KIND=4),INTENT(IN)::NODEID(-7:NIN), NWALLID(LNODE,4)
  INTEGER(KIND=4),INTENT(OUT)::NN,ND(0:NLMAX)
  REAL(KIND=8),INTENT(IN)::XIN(NIN),YIN(NIN),ZIN(NIN)
  REAL(KIND=8),INTENT(IN)::XQ,YQ,ZQ
  REAL(KIND=8),INTENT(OUT)::W(NLMAX)

  INTEGER(KIND=4),INTENT(INOUT)::I,I2,IX,IY,IZ,IPOS,IX1,IY1,IZ1
  REAL(KIND=8),INTENT(INOUT)::RIAV,ETMP,DR,DR2,WWI
  
  
  NN=0
  ETMP=DEXP(-(1D0**2))

  CALL FSDOM%FINDCELL(XQ,YQ,ZQ,IX,IY,IZ,IPOS)

  DO IX1=IX-1,IX+1
    DO IY1=IY-1,IY+1
      DO IZ1=IZ-1,IZ+1

        IF((IX1.GE.0.AND.IX1.LT.FSDOM%CELLX).AND.&
          (IY1.GE.0.AND.IY1.LT.FSDOM%CELLY).AND.&
          (IZ1.GE.0.AND.IZ1.LT.FSDOM%CELLZ))THEN

          IPOS=IX1+IY1*FSDOM%CELLX+IZ1*FSDOM%CELLY*FSDOM%CELLX
          
          DO I2=1,FSDOM%CELL(IPOS,0)
            I=FSDOM%CELL(IPOS,I2)

            !! NO GHOST PARTICLES
            IF( (NODEID(I).LT.0) .OR. (NODEID(I).GT.10) ) CYCLE

            !! NO DISABLED PARTICLES
            IF( (NWALLID(I,2).EQ.-10) ) CYCLE

            DR=DSQRT((XIN(I)-XQ)**2 + (YIN(I)-YQ)**2 + (ZIN(I)-ZQ)**2)
            IF(DR.LT.RIAV)THEN
              DR2=DR/RIAV

              IF(IDWEI.EQ.1)THEN
                WWI=(DEXP(-(DR2**2))-ETMP)/(1D0-ETMP)
              ELSE
                WWI=1D0-6D0*(DR2)**2+8D0*(DR2)**3-3D0*(DR2)**4 
              ENDIF

              IF(WWI.LE.0D0)CYCLE
              NN=NN+1
              IF(NN.GT.NLMAX)THEN
                WRITE(8,'(" [ERR] INCREASE NLMAX IN WEIGHTF3_SHA")')
                STOP
              ENDIF
              ND(NN)=I
              W(NN)=WWI
            ENDIF
          ENDDO
            
        ENDIF

      ENDDO
    ENDDO
  ENDDO
  ND(0)=NN

END SUBROUTINE WEIGHTF4_SHA
!!----------------------- END WEIGHTF4_SHA ------------------------!!



